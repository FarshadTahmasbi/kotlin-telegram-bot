plugins {
    alias libs.plugins.kotlinx.binaryCompatibilityValidator
    alias libs.plugins.kotlin.jvm
    id("maven-publish")
}

group = "com.github.kotlintelegrambot"
version = "6.3.1-alpha1"


publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'telegram'
            version = version
            from components.java
        }
    }
}


dependencies {
    // Networking
    implementation libs.bundles.retrofit

    implementation libs.okhttp3.logging.interceptor

    implementation libs.kotlinx.coroutines.core

    // Testing
    testImplementation libs.bundles.junit
    testImplementation libs.assertj.core
    testImplementation libs.mockk
    testImplementation libs.okhttp3.mockwebserver
    testImplementation libs.kotlinx.coroutines.test
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts { archives sourcesJar }

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}
